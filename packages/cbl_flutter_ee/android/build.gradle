plugins {
    id "com.android.library"
    id "kotlin-android"
}

import org.apache.tools.ant.taskdefs.condition.Os

group 'com.terwesten.gabriel.cbl_flutter_ee'
version '1.0-SNAPSHOT'

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

android {
    namespace 'com.terwesten.gabriel.cbl_flutter_ee'
    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdk 22
    }
}

dependencies {
}

task activateCblFlutter(type: Exec) {
    commandLine 'dart', 'pub', 'global', 'activate', 'cbl_flutter', '^3.2.0-dev.1'

    standardOutput = new ByteArrayOutputStream()
    errorOutput = new ByteArrayOutputStream()
    ignoreExitValue = true

    doLast {
        if (executionResult.get().exitValue != 0) {
            throw new GradleException(
                "Failed to activate cbl_flutter:\n" +
                "${standardOutput.toString().trim()}\n" +
                "${errorOutput.toString().trim()}"
            )
        }
    }
}

task installLibraries(type: Exec) {
    workingDir "$projectDir/.."
    commandLine 'cbl_flutter_install', 'android'
}

tasks.installLibraries.dependsOn(activateCblFlutter)
tasks.preBuild.dependsOn(installLibraries)

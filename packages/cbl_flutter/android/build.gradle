import org.apache.tools.ant.taskdefs.condition.Os

group 'com.terwesten.gabriel.cbl_flutter'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 30

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 22
    }

    externalNativeBuild {
        ndkBuild {
            path "Android.mk"
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task installBinaries(type: Exec) {
    workingDir file("$projectDir/..")

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'C:/Program Files/Git/bin/bash', './tool/install_binaries.sh', 'android'
    } else {
        commandLine './tool/install_binaries.sh', 'android'
    }
}

tasks.preBuild.dependsOn(installBinaries)
